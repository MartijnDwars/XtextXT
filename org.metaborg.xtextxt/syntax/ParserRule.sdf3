module ParserRule

imports
  
  Common
  
context-free syntax
  
  Rule.ParserRule = ID Returns ":"
    Token
  ";"
  
  Returns.None       =
  Returns.Returns    = "returns" TypeRef
  
  TypeRef.MetaModel  = ID '::' ID
  TypeRef.Classifier = ID
  
  Token          = '(' Token ')' {bracket}
  Token.Alt      = Token '|' Token {right}
  Token.Set      = Token '&' Token {right}
  Token.Group    = Token Token {right}
  Token.Optional = Token "?"
  Token.Multiple = Token "+"
  Token.Any      = Token "*"
  Token          = Terminal
  
  Terminal.Assign   = ID Operator AssignableTerminal
  Terminal.Keyword  = STRING
  Terminal.Call     = ID
  Terminal.Action   = "{" ID "}"
  Terminal.XRef     = "[" ID XRefTerminal "]"
  
  AssignableTerminal         = "(" AssignableTerminal ")" {bracket}
  AssignableTerminal.Alt     = AssignableTerminal '|' AssignableTerminal {right}
  AssignableTerminal.Keyword = STRING
  AssignableTerminal.Call    = ID
  AssignableTerminal.XRef    = "[" ID XRefTerminal "]"
  
  XRefTerminal.Id      = 
  XRefTerminal.Call    = "|" ID
  XRefTerminal.Keyword = "|" STRING
  
  Operator.EqualsAssign = "="
  Operator.ListAssign   = "+="
  Operator.BoolAssign   = "?=" 

context-free priorities

  {Token.Optional Token.Multiple Token.Any Terminal.Assign}
> Token.Group
> Token.Set
> Token.Alt
