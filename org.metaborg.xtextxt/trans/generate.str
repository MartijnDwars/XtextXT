module generate

imports

	include/Xtext
	lib/sdf/TemplateLang
	lib/sdf/TemplateLang-pp
  lib/sdf/pp/TemplateLang-pp
  trans/xtext
  gen/common
  gen/expand
  
rules 
  
  generate-sdf:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|$[sdf3])> path
    ; result   := <transform ; pp-SDF> selected
  
  refactor-sdf:
    (user-input, selected, position, ast, path, project-path) -> (changes, [], [], [])
    with
      changes := <map(!(<id>, <transform ; pp-SDF>))> selected
  
  transform:
  	selected -> result
  	where
      expanded := <interpolate-td(try(repeat(expand)))> selected
    ; result   := <transform-All <+ map(transform-All)> expanded
  
  transform-All =
    transform-Grammar <+ transform-Rule <+ transform-Token
  
  transform-Grammar:
    Grammar(name, mixin, xrules) -> Module(Unparameterized(<last> name), [], sections)
    where
      prods    := <map(transform-Rule)> xrules
    ; sections := [
        SDFSection(
      	 ContextFreeSyntax(
      		  prods
    		  )
  		  )
		  ]
  
  transform-Rule:
    ParserRule(name, _, body) -> SdfProduction(SortDef(name), Rhs(rhs), NoAttrs())
    where
      rhs := <transform-Token ; listify> body
  
  transform-Token =
    transform-Group <+ transform-Cardinality <+ transform-Keyword <+ transform-Call
  
  transform-Group:
    Group(tokens) -> result
    where
      result := <map(transform-Token)> tokens
  
  transform-Cardinality:
    Optional(x) -> Opt(<transform-Token> x)
  
  transform-Cardinality:
    Multiple(x) -> Iter(<transform-Token> x)
  
  transform-Cardinality:
    Any(x) -> IterStar(<transform-Token> x)
  
  transform-Keyword:
  	Keyword(x) -> Lit(x)
  
  transform-Call:
    Call(x) -> Sort(x)
